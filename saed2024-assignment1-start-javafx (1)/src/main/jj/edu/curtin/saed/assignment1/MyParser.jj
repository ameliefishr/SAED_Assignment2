PARSER_BEGIN(MyParser)

package edu.curtin.saed.assignment1;

import edu.curtin.saed.assignment1.*;
import java.util.*;
import java.io.*; 
import java.io.FileNotFoundException; 

public class MyParser {
    // Class-level variables to store parsed data
    private static int rows;
    private static int cols;
    private static int startRow;
    private static int startCol;
    private static int goalRow;
    private static int goalCol;
    private static String name;
    private static List<String> coordinates = new ArrayList<>();
    private static String message;
    private static Game game; // game instance

    public static void main(String[] args) throws ParseException, FileNotFoundException {
        FileInputStream fileInputStream = new FileInputStream("input.dsl");
        MyParser parser = new MyParser(fileInputStream);
        parser.StartupDeclarations();
        
        initializeGame();
        

        System.out.println("Parsed Game Variables:");
        System.out.println("Size: " + rows + " x " + cols);
        System.out.println("Start: (" + startRow + "," + startCol + ")");
        System.out.println("Goal: (" + goalRow + "," + goalCol + ")");
        System.out.println("Parsed Item: " + name);
        System.out.println("Coordinates: " + coordinates);
        System.out.println("Message: " + message);
    }

    private static void initializeGame() {
        Location playerLocation = new Location(startRow, startCol); 
        Location goalLocation = new Location(goalRow, goalCol);

        game = new Game(playerLocation, goalLocation, new ArrayList<>(), new ArrayList<>());
        
        System.out.println("Game Initialized:");
        System.out.println("Player Start Location: " + playerLocation);
        System.out.println("Goal Location: " + goalLocation);
    }
}

PARSER_END(MyParser)

SKIP : {
    " " | "\t" | "\r" | "\n" // Skip spaces, tabs, and newlines
}

// Token definitions
TOKEN : {
    <SIZE: "size"> |
    <START: "start"> |
    <GOAL: "goal"> |
    <ITEM: "item"> |
    <AT: "at"> |
    <MESSAGE: "message"> |
    <LPAREN: "("> |
    <RPAREN: ")"> |
    <LBRACE: "{"> |
    <RBRACE: "}"> |
    <COMMA: ","> |
    <STRING_LITERAL: "\"" (~["\""])* "\"" > |
    <NUMBER: (["0"-"9"])+> 
}

// Parser definitions
void StartupDeclarations() : 
{ 
} 
{
    SizeDeclaration() 
    StartDeclaration() 
    GoalDeclaration()
    ItemDeclaration()
}

// Parse size declaration: size (rows, cols)
void SizeDeclaration() : 
{
    Token rowToken;
    Token colToken;
} 
{
    <SIZE> <LPAREN> rowToken = <NUMBER> <COMMA> colToken = <NUMBER> <RPAREN>
    {
        rows = Integer.parseInt(rowToken.image);
        cols = Integer.parseInt(colToken.image);
        if (rows < 1 || cols < 1) {
            throw new ParseException("Invalid size values. Must be at least 1.");
        }
    }
}

// Parse start declaration: start (startRow, startCol)
void StartDeclaration() : 
{
    Token startRowToken;
    Token startColToken;
} 
{
    <START> <LPAREN> startRowToken = <NUMBER> <COMMA> startColToken = <NUMBER> <RPAREN>
    {
        startRow = Integer.parseInt(startRowToken.image);
        startCol = Integer.parseInt(startColToken.image);
        if (startRow < 0 || startRow >= rows || startCol < 0 || startCol >= cols) {
            throw new ParseException("Invalid start location. Must be within grid bounds.");
        }
    }
}

// Parse goal declaration: goal (goalRow, goalCol)
void GoalDeclaration() : 
{
    Token goalRowToken;
    Token goalColToken;
} 
{
    <GOAL> <LPAREN> goalRowToken = <NUMBER> <COMMA> goalColToken = <NUMBER> <RPAREN>
    {
        goalRow = Integer.parseInt(goalRowToken.image);
        goalCol = Integer.parseInt(goalColToken.image);
        if (goalRow < 0 || goalRow >= rows || goalCol < 0 || goalCol >= cols) {
            throw new ParseException("Invalid goal location. Must be within grid bounds.");
        }
    }
}

// Parse item declaration: item "name" { at (1,1), message "text" }
void ItemDeclaration() : 
{
    Token nameToken; 
    List<String> locList = new ArrayList<>();
} 
{
    <ITEM> nameToken = <STRING_LITERAL> <LBRACE> AtDeclaration(locList) MessageDeclaration() <RBRACE>
    {
        name = nameToken.image.substring(1, nameToken.image.length() - 1); 
        coordinates = locList; 
    }
}

// list of coordinates
void AtDeclaration(List<String> locList) : 
{
} 
{
    <AT> CoordinateList(locList)
}

// parse list of coordinates (allow multiple pairs)
void CoordinateList(List<String> locList) : 
{
} 
{
    Coordinate(locList) ( <COMMA> Coordinate(locList) )*
}

// parse individual coordinates 
void Coordinate(List<String> locList) : 
{
    Token x; 
    Token y;
} 
{
    <LPAREN> x = <NUMBER> <COMMA> y = <NUMBER> <RPAREN>
    {
        locList.add("(" + x.image + "," + y.image + ")"); 
    }
}

// handle message declaration
void MessageDeclaration() : 
{
    Token messageToken;
} 
{
    <MESSAGE> messageToken = <STRING_LITERAL> 
    {
        message = messageToken.image.substring(1, messageToken.image.length() - 1); 
    }
}